; #############################################################################
; # Atlas 2 Kernel - String routines                                          #
; # Created: 28.01.2013                                                       #
; # Changelog:                                                                #
; # 28.01.2013, plusmid - file created, basic routines                        #
; # 31.01.2013, plusmid - added strncpy, strpack, strunpack, pstrlen, ustrlen #
; # 02.02.2013, plusmid - strlen optimized, strncpy fixed, strpack/unpack is  #
; #                       broken                                              #
; # 02.02.2013, bungao - strpack/strunpack fixed and tested                   #
; # 03.02.2013, VladVP - added strcpyfast which is merely an optimization of  #
; #                      strcpy, and optimized strpack and strunpack          #
; # 04.02.2013, bungao - merged in optimizations, added labels                #
; # 14.02.2013, VladVP - added strpcpy and strpncpy                           #
; #############################################################################


; strlen - calculates the length of a string
; A -> pointer to the string
; A <- length of the string in words
:strlen
	SET PUSH, A

	SUB A, 1

:strlen_loop
	ADD A, 1
	IFN [A], 0
		SET PC, strlen_loop

	SUB A, POP
	SET PC, POP

; strcpy - copies a string from one address to another
; A -> source pointer
; B -> destination pointer
; A <- copied words
:strcpy
	SET PUSH, I
	SET PUSH, J
	
	SET I, A
	SET J, B
	
:strcpy_loop
	STI [J], [I]
	IFN [I], 0
	SUB PC, 3 ; :strcpy_loop
	
	STI [J], [I]
	
	SET J, POP
	SUB I, A
	SET A, I
	SET I, POP
	SET PC, POP

	
; strncpy - same as strcpy but with length limitation
; A -> source string
; B -> destination string
; C -> max length
:strncpy
	SET PUSH, I
	SET PUSH, J
	SET PUSH, C

	SET I, A
	SET J, B
	
:strcpyn_loop
	STI [J], [I]
	SUB C, 1
	IFE C, 0
	ADD PC, 2 ; strncpy_loop
	IFN [I], 0
		SUB PC, 6 ; strncpy_loop

:strncpy_end
	SET C, POP
	SET I, POP
	SET J, POP
	SET PC, POP


; strpack - packs a string
; A -> source string
; B -> destination of the packed string
:strpack
	SET PUSH, A
	SET PUSH, B
	
:strpack_loop
	SET [B], [A]
	SHL [B], 8
	IFE [A], 0
	ADD PC, 7 ; strpack_end
	ADD A, 1
	IFE [A], 0
	ADD PC, 4 ; strpack_end
	BOR [B], [A]
	ADD A, 1
	ADD B, 1
	SUB PC, 0xB ; strpack_loop
	
:strpack_end
	SET B, POP
	SET A, POP
	SET PC, POP

; strunpack - unpacks a string packed by strpack and/or strpackfast
; A -> packed source string
; B -> destination string
:strunpack
	SET PUSH, A
	SET PUSH, B
	SUB B, 1
	
:strunpack_loop	
	ADD B, 1
	SET [B], [A]
	SHR [B], 8
	IFE [B], 0
	ADD PC, 7 ; strunpack_end
	ADD B, 1
	SET [B], [A]
	AND [B], 0x00FF
	ADD A, 1
	IFN [B], 0
	SUB PC, 0xC : strunpack_loop
	
:strunpack_end
	SET B, POP
	SET A, POP
	SET PC, POP

	
; pstrlen - calculates the length of a string (packed)
; A -> string
; B <- packed length
; ToDo: TEST!
:pstrlen
	JSR strlen
	ADD A, 2 ; +1 for the tailing 0x00 +1 for the packed tailing 0x0000
	SHR A, 1
	SET PC, POP


; ustrlen - calculates the length of a packed string (unpacked)
; A -> string
; A <- length
; ToDo: TEST!
:ustrlen
	SET PUSH, B
	SET PUSH, C

	SET C, 0

:ustrlen_loop
	SET B, [A] ; add 2 to C while the upper byte of [A]!=0x00
	AND B, 0xFF00
	ADD A, 1
	ADD C, 2
	IFN B, 0x0000
		SET PC, ustrlen_loop

	SUB A, 1 ; subtract the number of 0x00 we have counted
	SUB C, 1
	IFE [A], 0x0000
		SUB C, 1

	SET A, C

	SET C, POP
	SET B, POP
	SET PC, POP


; strpcpy - copies a lenght-prefixed string from one address to another
; A -> source pointer
; B -> destination pointer
:strpcpy
	SET I, A
	SET J, B
	
	SET PUSH, I
	SUB [SP], 1
	ADD J, [I]
	ADD I, [I]
	STD [J], [I]
	IFN I, PEEK
	SUB PC, 3
	SET I, POP ;any better alternatives?
	ADD I, 1
	ADD J, 1
	SET PC, POP


; strpncpy - same as strpcpy but with length limitation
; A -> source string
; B -> destination string
; C -> max length
:strpncpy
	SET I, A
	SET J, B
	
	SET PUSH, I
	SUB [SP], 1
	
	IFG C, [I]
	ADD PC, 5
	
	ADD J, C
	ADD I, C
	ADD PC, 2
	
	ADD J, [I]
	ADD I, [I]
	
	STD [J], [I]
	IFN I, PEEK
	SUB PC, 3
	
	SET I, POP ;any better alternatives?
	ADD I, 1
	ADD J, 1
	SET PC, POP